// ext makes method callable project wide
def readPropertiesFile(file_path)
{
    println("READING properties file: ${file_path}")
    def properties_file = file(file_path)
    def Properties props = new Properties()
    if (properties_file.canRead()) {
        props.load(new FileInputStream(properties_file))
    }
    props.each { key, val ->
        println("    PROPERTY ${key}=${val} loaded")
    }
    return props;
}

def readCIProperties(ci_config_dir)
{
    def properties_file = file(ci_config_dir + "/BAMBOO-VARS.properties")
    def Properties props = new Properties()
    if (properties_file.canRead()) {
        props.load(new FileInputStream(properties_file))
    }
    else {
        props.put("build.version.name.postfix", "")
        props.put("build.number", "")
        props.put("build.branch.name", "")
        props.put("build.distribution.dropbox.folder", "")
    }
    props.each { key, val ->
        println("PROPERTY ${key}=${val} loaded")
    }
    return props;
}

def updateBuildVersion(ci_config_dir) {
    Properties ci_props = readCIProperties(ci_config_dir)
    def postfix = ci_props.get("build.version.name.postfix")
    return "-${postfix}"
}

def buildVersionedOutputApkFile(ci_config_dir, output, platform) {
    Properties ci_props = readCIProperties(ci_config_dir)
    def buildNumber = ci_props.get("build.number")
    def branchName = ci_props.get("build.branch.name")
    def new_output_name = output.outputFile.name.replace(".apk", "-${platform}-${branchName}-${buildNumber}.apk")
    return new File(new_output_name)
}

def createUploadToDropBoxTask(ci_config_dir, tools_dir, output, platform) {
    Properties ci_props = readCIProperties(ci_config_dir)
    def script_path =  "${tools_dir}/dropbox_uploader.py"
    def dropboxFolder = ci_props.get("build.distribution.dropbox.folder").replace("\"", "")
    return tasks.create("uploadApkToDropbox-${output.outputFile.name}", Exec) {
        commandLine 'python', script_path,
                "-f=${output.outputFile}",
                "-o=/${dropboxFolder}/${platform}"
    }
}

//Export methods by turning them into closures
ext{
    createUploadToDropboxTask = this.&createUploadToDropBoxTask
    buildVersionedOutputApkFile = this.&buildVersionedOutputApkFile
    updateBuildVersion = this.&updateBuildVersion
    readPropertiesFile = this.&readPropertiesFile
}
